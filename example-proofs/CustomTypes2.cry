/*
 * Cryptol-level stuff for modeling the custom type.
 *
 * The Rust representation of the type is a tuple struct with two
 * members, both u32.
 *
 * The native representation is a pair ([32], [32]).
 *
 * The Soroban-level representation is a Values::VecObject::T that
 * points to two Values::U32::T.
 */

module CustomTypes2 where

import Option as Option
import Values as Values
import submodule Values::Symbol as Values::Symbol
import submodule Values::VecObject as Values::VecObject
import submodule Values::U32 as Values::U32
import submodule Values::Raw as Values::Raw

type T = ([32], [32])

/*
 * A default value.
 */
default_value : T
default_value = (0, 0)

/*
 * Check if the underlying VecObject is well formed.
 */
valid_with_heap : Values::VecObject::T -> Values::HostHeap -> Bool
valid_with_heap state_val heap =
   Values::vecobj_valid_with_heap state_val heap /\
   Values::vecobj_len state_val heap == 2 /\
   Values::raw_is_u32 count_val /\
   Values::raw_is_u32 last_val
   where
      count_val = Values::vecobj_get state_val 0 heap
      last_val = Values::vecobj_get state_val 1 heap

/*
 * Extract the native representation from a VecObject.
 *
 * Should have already asserted that the elements are U32.
 * Crashes if they aren't.
 */
val_to_native : Values::VecObject::T -> Values::HostHeap -> T
val_to_native state_val heap =
   (count_native, last_native)
   where
      count_raw = Values::VecObject::get state_val 0 heap
      last_raw = Values::VecObject::get state_val 1 heap
      count_optval = Values::U32::from_raw count_raw
      last_optval = Values::U32::from_raw last_raw
      count_val =
         if Option::is_some count_optval then Option::unsafe_unwrap count_optval
         // XXX this is going off in spite of assertions to the contrary,
         // should therefore be only on doomed branches and ok to provide a
         // placeholder value instead... but needs to be looked into further
         //else error "val_to_native: count not U32::T"
         else Values::U32::from_native 0
      last_val =
         if Option::is_some last_optval then Option::unsafe_unwrap last_optval
         // XXX same here
         //else error "val_to_native: last not U32::T"
         else Values::U32::from_native 0
      count_native = Values::U32::to_native count_val
      last_native = Values::U32::to_native last_val
