module Symbol where

import Option as Option
import RawVal as RawVal
import Tag as Tag
import Body as Body

small_tag : Tag::T
small_tag = Tag::symbol_small

object_tag : Tag::T
object_tag = Tag::symbol_object

submodule General where

   submodule Small where
      type T = Small
      newtype Small = { raw : RawVal::T }

   // end of Small

   submodule Object where
      type T = Object
      newtype Object = { raw : RawVal::T }
   // end of Object

   submodule Wrapper where
      type T = Wrapper
      newtype Wrapper = { raw : RawVal::T }

import submodule General::Small as General::Small
import submodule General::Object as General::Object
import submodule General::Wrapper as Wrapper

type SmallT = General::Small::T
type ObjectT = General::Object::T

small_unsafe_from_raw : RawVal::T -> SmallT
small_unsafe_from_raw rv = General::Small::Small { raw = rv }

small_from_body : Body::T -> SmallT
small_from_body body = small_unsafe_from_raw (RawVal::from_body_and_tag body small_tag)

small_from_string : {n} (n <= 9) => String n -> SmallT
small_from_string s = small_from_body (zext (join (map encode_char s)))

small_to_raw : SmallT -> RawVal::T
small_to_raw w = w.raw

general_unsafe_from_raw : RawVal::T -> Symbol
general_unsafe_from_raw rv = Wrapper::Wrapper { raw = rv }

general_from_small : SmallT -> Wrapper::T
general_from_small s = general_unsafe_from_raw (small_to_raw s)

encode_char : Char -> [6]
encode_char ch =
  if ch == '_' then 1
  | elem ch ['0'..'9'] then 2 + drop (ch - '0')
  | elem ch ['A'..'Z'] then 12 + drop (ch - 'A')
  | elem ch ['a'..'z'] then 38 + drop (ch - 'a')
  else error
    ("symbol bad char: encountered " # [ch]
      # ", supported range [a-zA-Z0-9_]")

type T = Symbol

type Symbol = Wrapper::T

short : {n} (n <= 9) => String n -> Symbol
short s = general_from_small (small_from_string s)

general_valid_raw : RawVal::T -> Bit
general_valid_raw rv = RawVal::has_tag small_tag rv
                    \/ RawVal::has_tag object_tag rv

from_raw : RawVal::T -> Option::T Symbol
from_raw rv = Option::mk (general_valid_raw rv) (general_unsafe_from_raw rv)

valid : Symbol -> Bit
valid w = general_valid_raw w.raw
