enable_experimental;

include "list.saw";

/*
 * Cryptol modules we're using that are used after initialization and
 * therefore need to get passed around.
 */
typedef SorobanCry = {
  Values : CryptolModule,
  //Void : CryptolModule,
  //Bool_ : CryptolModule,
  //Symbol : CryptolModule,
  //RawVal : CryptolModule,

  Storage : CryptolModule
};

/*
 * Symbols extracted from the MIR
 */
typedef SorobanAdt = {
  // from the soroban_env_guest crate
  Guest : (() -> MIRAdt),

  // from the soroban_sdk crate
  Env : (() -> MIRAdt),
  MaybeEnv : (() -> MIRAdt),
  Storage : (() -> MIRAdt),
  Symbol : (() -> MIRAdt),

  // from the soroban_env_common crate
  Bool_ : (() -> MIRAdt),
  RawVal : (() -> MIRAdt),
  SymbolVal : (() -> MIRAdt),
  Void : (() -> MIRAdt)
};

typedef SorobanSpecAPI = {

  storage : {
    state : (Term /* Storage */ -> MIRSetup ()),
    has : (Term /* Storage */ -> Term /* k */ -> Term /* Bit */),
    get : (Term /* Storage */ ->
      Term /* k */ -> Type /* = v */ -> Term /* Option (Option v) */),
    set : (Term /* Storage */ ->
      Term /* k */ -> Term /* v */ -> Term /* Storage */)
  },

  fresh : (String -> Type -> MIRSetup Term),

  mir_value : {
    Guest : (() -> MIRValue),
    Env : (() -> MIRValue),
    MaybeEnv : (() -> MIRValue),
    Storage : (() -> MIRValue)
  },
  to_mir_value : (Term -> MIRValue),
  to_mir_type : (Type -> MIRType)

};

/*
 * Args passed to verify
 */
typedef SorobanVerifyArgs = {
  fun : String,
  overrides : [MIRSpec],
  path_sat : Bool,
  spec : MIRSetup (),
  proof : ProofScript ()
};

/*
 * Material returned by the init function for use by the actual specs:
 *    - Cryptol modules
 *    - references to MIR symbols from the SDK
 *    - spec wrapper function
 *    - verify wrapper function
 */
typedef SorobanAPI = {
  cry: SorobanCry,
  adt : SorobanAdt,
  spec : (String -> (SorobanSpecAPI -> MIRSetup ()) -> MIRSetup ()),
  verify : (SorobanVerifyArgs -> TopLevel MIRSpec)
};

/*
 * Function to initialize the stuff in here.
 *
 * Arguments are:
 *    mod : MirModule
 *    features : [String]
 *
 * mod is the results of mir_load_module on the linked_mir.json file
 * containing the stuff we're trying to verify.
 *
 * The features list is a list of arbitrary strings naming things the
 * rust code is using; it prevents us from looking for MIR symbols
 * that don't exist.
 */
soroban_init <- do {

  /*
   * Load the cryptol modules.
   */

  Util <- cryptol_load "Util.cry";
  Option <- cryptol_load "Option.cry";

  //Void <- cryptol_load "Void.cry";
  //Bool_ <- cryptol_load "Bool.cry";
  //U32 <- cryptol_load "U32.cry";
  //Symbol <- cryptol_load "Symbol.cry";
  //RawVal <- cryptol_load "RawVal.cry";
  Values <- cryptol_load "Values.cry";
  // can't do this, alas
  //Void <- cryptol_extract_submodule Values "Void";
  //let Bool_ = cryptol_extract_submodule Values "Bool";
  //let U32 = cryptol_extract_submodule Values "U32";
  //let Symbol = cryptol_extract_submodule Values "Symbol";
  //RawVal <- cryptol_extract_submodule Values "Raw";

  Storage <- cryptol_load "Storage.cry";

  /*
   * I'm not sure what this layer of indirection buys us.
   */

  let soroban_init (mod : MIRModule) (features : [String])
  : TopLevel SorobanAPI = do {

    state <- do {
      storage <- declare_ghost_state "Soroban::storage";
      return {
        storage = storage
      };
    };

    let soroban_cry : SorobanCry = {
      Values = Values,
      //Void = Void,
      //Bool_ = Bool_,
      //Symbol = Symbol,
      //RawVal = RawVal,

      Storage = Storage
    };

    let feat_implies feats (x, y) =
      if string_list.elem x feats then concat [y] feats else feats;

    let features = list.foldl feat_implies features [
      ("storage", "bool_val"),
      ("storage", "void_val"),
      ("symbol", "maybe_env")
    ];

    let soroban_adt : SorobanAdt =
      /*
       * Tools for looking up stuff in the MIR.
       */
      let find adt_name =
        let adt = mir_find_adt mod adt_name [] in
        \() -> adt
      in
      let find_when feat adt_name =
        if string_list.elem feat features then
          find adt_name
        else
          \() -> run (fail (str_concats [
            "Feature \"", feat, "\" is required for type ", adt_name
          ]))
      in

      /*
       * Bind handles for various SDK types.
       */
      {
        // from the soroban_env_guest crate
        Guest = find
          "soroban_env_guest::guest::Guest",

        // from the soroban_sdk crate
        Env = find
          "soroban_sdk::env::Env",
        MaybeEnv = find_when "maybe_env"
          "soroban_sdk::env::MaybeEnv",
        Storage = find_when "storage"
          "soroban_sdk::storage::Storage",
        Symbol = find_when "symbol"
          "soroban_sdk::symbol::Symbol",

        // from the soroban_env_common crate
        Bool_ = find_when "bool_val"
          "soroban_env_common::raw_val::Bool",
        RawVal = find
          "soroban_env_common::raw_val::RawVal",
        SymbolVal = find_when "symbol"
          "soroban_env_common::symbol::Symbol",
        Void = find_when "void_val"
          "soroban_env_common::raw_val::Void"
      };

    /*
     * Bind mir_values for things.
     */
    let mir_value =
      let mir_value_Guest () =
        mir_struct_value (soroban_adt.Guest ()) [] in
      let mir_value_Env () =
        mir_struct_value (soroban_adt.Env ()) [mir_value_Guest ()] in
      let mir_value_MaybeEnv () =
        mir_struct_value (soroban_adt.MaybeEnv ()) [mir_value_Guest ()] in
      let mir_value_Storage () =
        mir_struct_value (soroban_adt.Storage ()) [mir_value_Env ()] in
      {
        Guest = mir_value_Guest,
        Env = mir_value_Env,
        MaybeEnv = mir_value_MaybeEnv,
        Storage = mir_value_Storage
      };

    /*
     * Hook for dynamic type dispatch on the value types.
     */
    let type_eq (t1 : Type) (t2 : Type) : Bool =
      let s1 = show t1 in
      let s2 = show t2 in
      eval_bool {{ Util::lenPolyEq s1 s2 }};

    /*
     * Convert from a specific value type to Values::RawVal::T
     *
     * to_raw : (t : Type) -> t -> Values::RawVal::T
     */
    let to_raw (t : Type) : Term /* t -> RawVal */ =
      if type_eq t {| [32] |} then
        {{ Values::u32_to_raw }}
      else
        {{ \(s : t) -> s.raw }};

    /*
     * Convert from a Values::RawVal::T to a specific value type
     * (type is passed as the first argument)
     *
     * Returns None if the raw value is not actually a value
     * of the right type.
     *
     * from_raw : (t : Type) -> Values::RawVal::T -> Option t
     */
    let from_raw (t : Type) : Term =
      if type_eq t {| [32] |} then
        {{ Values::u32_from_raw }}
      else if type_eq t {| Bool_::T |} then
        {{ Values::bool_from_raw }}
      else if type_eq t {| Void::T |} then
        {{ Values::void_from_raw }}
      else if type_eq t {| Symbol::T |} then
        {{ Values::symbol_from_raw }}
      else
        run (fail (str_concat (show t) " cannot be converted from RawVal"));

    /*
     * Check if a value of a value type is actually a valid value of
     * that type. In theory, we shouldn't ever attach types to invalid
     * values; we shouldn't ever construct any and potentially-garbage
     * values from outside should appear first as Values::RawVal and get
     * checked before being converted to a specific value type.
     *
     * However, the Soroban SDK rust code plays fast and loose with
     * unsafe casts and we want to interoperate with it as much as
     * possible.
     *
     * valid : (t : Type) -> t -> Bit
     */
    let type_valid (t : Type) : Term /* t -> Bit */ =
      if type_eq t {| Values::BoolT |} then
        {{ Bool_::valid }}
      else if type_eq t {| Values::VoidT |} then
        {{ Void::valid }}
      else if type_eq t {| Values::SymbolT |} then
        {{ Symbol::valid }}
      else
        // default to no conditions
        {{ \_ -> True }};

    /*
     * Convert a value of a value type (recall the value types are
     * Cryptol types and have Cryptol values) to a mir_term.
     *
     * XXX: should this be converting to "native" (plain cryptol
     * values that are not Soroban SDK tagged values) which then
     * presumably get converted back to the Rust tagged implementation
     * on the MIR side, or should we be passing the representation
     * across in all cases and not just the raw case?
     */
    rec to_mir_value x =
      let t = type x in
      if type_eq t {| Values::RawT |} then
        mir_struct_value (soroban_adt.RawVal ()) [
          mir_term {{ x.payload }}
        ]
      else if type_eq t {| Values::BoolT |} then
        mir_struct_value (soroban_adt.Bool_ ()) [
          to_mir_value {{ x.raw }}
        ]
      else if type_eq t {| Values::VoidT |} then
        mir_struct_value (soroban_adt.Void ()) [
          to_mir_value {{ x.raw }}
        ]
      else if type_eq t {| Values::SymbolT |} then
        mir_struct_value (soroban_adt.Symbol ()) [
          mir_value.MaybeEnv (),
          mir_struct_value (soroban_adt.SymbolVal ()) [
            to_mir_value {{ x.raw }}
          ]
        ]
      else
        // fall back to mir_term
        mir_term x;

    /*
     * Extract the MIR-level type for type T.
     *
     * This function is the type-level version of to_mir_value, and
     * uses the same types as returned by to_native.
     *
     * XXX: except that's wrong... and we maybe need name reform,
     * because there's _three_ types for any given Soroban value:
     *    - the Rust type, which is something out of the MIR;
     *    - the Cryptol type, which matches the structure of the MIR type;
     *    - the native SAW and/or Cryptol representation of the value;
     *
     * and I think we're playing fast and loose with the distinction
     * between the last two, which is bad.
     */
    let to_mir_type t =
      if type_eq t {| [32] |} then
        mir_u32
      else if type_eq t {| Values::RawT |} then
        mir_adt (soroban_adt.RawVal ())
      else if type_eq t {| Values::BoolT |} then
        mir_adt (soroban_adt.Bool_ ())
      else if type_eq t {| Values::VoidT |} then
        mir_adt (soroban_adt.Void ())
      else if type_eq t {| Values::SymbolT |} then
        mir_adt (soroban_adt.Symbol ())
      else
        run (fail (str_concat (show t) " cannot be converted to MIRType"));

    /*
     * Spec for the storage interface.
     *
     * For now at least the Cryptol-level storage interface is written
     * purely in terms of Values::Raw::T and the type wrapping happens
     * here. It's possible that should change, but we'll see.
     */
    let spec_storage = {

      state = \storage -> do {
        mir_ghost_value state.storage storage;
      },

      has = \storage k ->
        let k_to_raw = to_raw (type k) in
        {{ Storage::has storage (k_to_raw k) }},

      get = \storage k tv ->
        let k_to_raw = to_raw (type k) in
        let v_from_raw = from_raw tv in
        {{ Option::fmap
            (Storage::get storage (k_to_raw k))
            v_from_raw }},

      set = \storage k v ->
        let k_to_raw = to_raw (type k) in
        let v_to_raw = to_raw (type v) in
        {{ Storage::set storage (k_to_raw k) (v_to_raw v) }}

    };

    /*
     * Wrapper for contract specs.
     */
    let soroban_spec spec_name spec = do {

      spec {

        storage = spec_storage,

        /*
         * Generate a fresh var of the requested value type.
         */
        fresh = \name t -> do {
          x <- mir_fresh_cryptol_var (str_concats [spec_name, "::", name]) t;
          let valid = type_valid t;
          mir_assert {{ valid x }};
          return x;
        },

        mir_value = mir_value,
        to_mir_value = to_mir_value,
        to_mir_type = to_mir_type

      };

    };

    let sdk_assume feat name spec =
      if string_list.elem feat features then
        list.sequence [mir_unsafe_assume_spec mod name (soroban_spec name spec)]
      else
        return [];

    /*
     * Construct the "overrides" (checked or admitted SAW theorems)
     * for the SDK interfaces.
     */
    sdk_overridess <-

      let adt_Infallible = mir_find_adt mod "core::convert::Infallible" [] in
      let adt_ResultInfallible (t : MIRType) =
        mir_find_adt mod "core::result::Result" [t, mir_adt adt_Infallible] in

      let return_ok (x : Term) : MIRSetup () =
        mir_return
          (mir_enum_value
            (adt_ResultInfallible (to_mir_type (type x)))
            "Ok"
            [to_mir_value x])
      in

      list.sequence [

        sdk_assume "storage" "soroban_sdk::env::{impl#12}::has_contract_data"
          (\(s : SorobanSpecAPI) -> do {

            storage <- s.fresh "storage" {| Storage::T |};

            self <- mir_alloc (mir_adt (soroban_adt.Env ()));
            mir_points_to self (s.mir_value.Env ());

            k <- s.fresh "k" {| Values::RawT |};

            s.storage.state storage;

            mir_execute_func [self, s.to_mir_value k];

            s.storage.state storage;

            return_ok {{ Values::bool_from_native (Storage::has storage k) }};

          }),

        sdk_assume "storage" "soroban_sdk::env::{impl#12}::get_contract_data"
          (\(s : SorobanSpecAPI) -> do {

            storage <- s.fresh "storage" {| Storage::T |};

            self <- mir_alloc (mir_adt (soroban_adt.Env ()));
            mir_points_to self (s.mir_value.Env ());

            k <- s.fresh "k" {| Values::RawT |};

            mir_precond {{ Storage::has storage k }};
            s.storage.state storage;

            mir_execute_func [self, s.to_mir_value k];

            s.storage.state storage;

            return_ok {{ (Storage::get storage k).value }};

          }),

        sdk_assume "storage" "soroban_sdk::env::{impl#12}::put_contract_data"
          (\(s : SorobanSpecAPI) -> do {

            storage <- s.fresh "storage" {| Storage::T |};

            self <- mir_alloc (mir_adt (soroban_adt.Env ()));
            mir_points_to self (s.mir_value.Env ());

            k <- s.fresh "k" {| Values::RawT |};
            v <- s.fresh "v" {| Values::RawT |};

            s.storage.state storage;

            mir_execute_func [self, s.to_mir_value k, s.to_mir_value v];

            s.storage.state {{ Storage::set storage k v }};

            return_ok {{ Values::unit }};

          })

      ];
    let sdk_overrides = list.concats sdk_overridess;

    /*
     * Hook for running verification
     */
    let soroban_verify (args : SorobanVerifyArgs) =
      mir_verify mod args.fun (concat sdk_overrides args.overrides)
        args.path_sat args.spec args.proof;

    return {
      cry = soroban_cry,
      adt = soroban_adt,
      spec = soroban_spec,
      verify = soroban_verify
    };

  };

  return soroban_init;

};
