enable_experimental;

include "list.saw";

/*
 * Load the cryptol modules.
 */

Util <- cryptol_load "Util.cry";
Option <- cryptol_load "Option.cry";
Values <- cryptol_load "Values.cry";
Store <- cryptol_load "Store.cry";

/*
 * Symbols extracted from the MIR
 */
typedef SorobanAdt = {
  // from the soroban_env_guest crate
  Guest : (() -> MIRAdt),

  // from the soroban_sdk crate
  Env : (() -> MIRAdt),
  MaybeEnv : (() -> MIRAdt),
  Storage : (() -> MIRAdt),
  Symbol : (() -> MIRAdt),

  // from the soroban_env_common crate
  Bool_ : (() -> MIRAdt),
  U32Val : (() -> MIRAdt),
  RawVal : (() -> MIRAdt),
  SymbolVal : (() -> MIRAdt),
  Void : (() -> MIRAdt)
};

typedef SorobanSpecAPI = {

  storage : {
    state : (Term /* Store */ -> MIRSetup ()),
    has : (Term /* Store */ -> Term /* k */ -> Term /* Bit */),
    get : (Term /* Store */ ->
      Term /* k */ -> Type /* = v */ -> Term /* Option (Option v) */),
    set : (Term /* Store */ ->
      Term /* k */ -> Term /* v */ -> Term /* Store */)
  },

  fresh : (String -> Type -> MIRSetup Term),

  mir_value : {
    Guest : (() -> MIRValue),
    Env : (() -> MIRValue),
    MaybeEnv : (() -> MIRValue),
    Storage : (() -> MIRValue)
  },
  to_mir_value : (Term -> MIRValue),
  to_mir_type : (Type -> MIRType)

};

/*
 * Args passed to verify
 */
typedef SorobanVerifyArgs = {
  fun : String,
  overrides : [MIRSpec],
  path_sat : Bool,
  spec : MIRSetup (),
  proof : ProofScript ()
};

/*
 * Material returned by the init function for use by the actual specs:
 *    - references to MIR symbols from the SDK
 *    - spec wrapper function
 *    - verify wrapper function
 *    - assume wrapper function
 */
typedef SorobanAPI = {
  adt : SorobanAdt,
  spec : (String -> (SorobanSpecAPI -> MIRSetup ()) -> MIRSetup ()),
  verify : (SorobanVerifyArgs -> TopLevel MIRSpec),
  assume : (SorobanVerifyArgs -> TopLevel MIRSpec)
};


////////////////////////////////////////////////////////////
// SDK value interface

    /*
     * Hook for dynamic type dispatch on the value types.
     */
    let type_eq (t1 : Type) (t2 : Type) : Bool =
      let s1 = show t1 in
      let s2 = show t2 in
      eval_bool {{ Util::lenPolyEq s1 s2 }};

    /*
     * Check if a value of a value type is actually a valid value of
     * that type. In theory, we shouldn't ever attach types to invalid
     * values; we shouldn't ever construct any and potentially-garbage
     * values from outside should appear first as Values::Raw and get
     * checked before being converted to a specific value type.
     *
     * However, the Soroban SDK rust code plays fast and loose with
     * unsafe casts and we want to interoperate with it as much as
     * possible.
     *
     * valid : (t : Type) -> t -> Bit
     */
    let valid (t : Type) : Term =
      if type_eq t {| Values::VoidT |} then
        {{ Values::void_valid }}
      else if type_eq t {| Values::BoolT |} then
        {{ Values::bool_valid }}
      else if type_eq t {| Values::U32T |} then
        {{ Values::u32_valid }}
      else if type_eq t {| Values::SymbolT |} then
        {{ Values::symbol_valid }}
      else if type_eq t {| Values::RawT |} then
        {{ Values::raw_valid }}
      else if type_eq t {| Store::T |} then
        // XXX: this gets passed here, but shouldn't be. ignore it
        // (this logic is for value types!)
        {{ \_ -> True }}
      else
        run (fail (str_concat (show t) " has no validity predicate"));

    /*
     * Convert from a specific value type to Values::Raw::T
     *
     * to_raw : (t : Type) -> t -> Values::Raw::T
     */
    let to_raw (t : Type) : Term =
      if type_eq t {| Values::VoidT |} then
        {{ Values::void_to_raw }}
      else if type_eq t {| Values::BoolT |} then
        {{ Values::bool_to_raw }}
      else if type_eq t {| Values::U32T |} then
        {{ Values::u32_to_raw }}
      else if type_eq t {| Values::SymbolT |} then
        {{ Values::symbol_to_raw }}
      else
        run (fail (str_concat (show t) " cannot be converted to Raw"));

    /*
     * Convert from a Values::Raw::T to a specific value type
     * (type is passed as the first argument)
     *
     * Returns None if the raw value is not actually a value
     * of the right type. Crashes if the type is wrong.
     *
     * from_raw : (t : Type) -> Values::Raw::T -> Option t
     */
    let from_raw (t : Type) : Term =
      if type_eq t {| Values::VoidT |} then
        {{ Values::void_from_raw }}
      else if type_eq t {| Values::BoolT |} then
        {{ Values::bool_from_raw }}
      else if type_eq t {| Values::U32T |} then
        {{ Values::u32_from_raw }}
      else if type_eq t {| Values::SymbolT |} then
        {{ Values::symbol_from_raw }}
      else
        run (fail (str_concat (show t) " cannot be converted from Raw"));


////////////////////////////////////////////////////////////
// init

/*
 * Function to initialize the stuff in here.
 *
 * Arguments are:
 *    mod : MirModule
 *    features : [String]
 *
 * mod is the results of mir_load_module on the linked_mir.json file
 * containing the stuff we're trying to verify.
 *
 * The features list is a list of arbitrary strings naming things the
 * rust code is using; it prevents us from looking for MIR symbols
 * that don't exist.
 */
soroban_init <- do {

  // XXX this is redundant and should be removed
  Util <- cryptol_load "Util.cry";

  /*
   * I'm not sure what this layer of indirection buys us.
   */

  let soroban_init (mod : MIRModule) (features : [String])
  : TopLevel SorobanAPI = do {

    state <- do {
      storage <- declare_ghost_state "Soroban::storage";
      return {
        storage = storage
      };
    };

    let feat_implies feats (x, y) =
      if string_list.elem x feats then concat [y] feats else feats;

    let features = list.foldl feat_implies features [
      ("storage", "bool_val"),
      ("storage", "void_val"),
      ("symbol", "maybe_env")
    ];

    let soroban_adt : SorobanAdt =
      /*
       * Tools for looking up stuff in the MIR.
       */
      let find adt_name =
        let adt = mir_find_adt mod adt_name [] in
        \() -> adt
      in
      let find_when feat adt_name =
        if string_list.elem feat features then
          find adt_name
        else
          \() -> run (fail (str_concats [
            "Feature \"", feat, "\" is required for type ", adt_name
          ]))
      in

      /*
       * Bind handles for various SDK types.
       */
      {
        // from the soroban_env_guest crate
        Guest = find
          "soroban_env_guest::guest::Guest",

        // from the soroban_sdk crate
        Env = find
          "soroban_sdk::env::Env",
        MaybeEnv = find_when "maybe_env"
          "soroban_sdk::env::MaybeEnv",
        Storage = find_when "storage"
          "soroban_sdk::storage::Storage",
        Symbol = find_when "symbol"
          "soroban_sdk::symbol::Symbol",

        // from the soroban_env_common crate
        Bool_ = find_when "bool_val"
          "soroban_env_common::raw_val::Bool",
        U32Val = find_when "u32_val"
          "soroban_env_common::num::U32Val",
        RawVal = find
          "soroban_env_common::raw_val::RawVal",
        SymbolVal = find_when "symbol"
          "soroban_env_common::symbol::Symbol",
        Void = find_when "void_val"
          "soroban_env_common::raw_val::Void"
      };

    /*
     * Bind mir_values for things.
     */
    let mir_value =
      let mir_value_Guest () =
        mir_struct_value (soroban_adt.Guest ()) [] in
      let mir_value_Env () =
        mir_struct_value (soroban_adt.Env ()) [mir_value_Guest ()] in
      let mir_value_MaybeEnv () =
        mir_struct_value (soroban_adt.MaybeEnv ()) [mir_value_Guest ()] in
      let mir_value_Storage () =
        mir_struct_value (soroban_adt.Storage ()) [mir_value_Env ()] in
      {
        Guest = mir_value_Guest,
        Env = mir_value_Env,
        MaybeEnv = mir_value_MaybeEnv,
        Storage = mir_value_Storage
      };

    /*
     * Convert a value of a value type (recall the value types are
     * Cryptol types and have Cryptol values) to a mir_term.
     *
     * The MIR term we want is the corresponding tree of constructors
     * from the Rust side. There are two ways to do this: we can
     * extract the semantic value from the Cryptol term (e.g. unit,
     * True, False, 131, "hello", or whatever), convert that to its
     * Rust/MIR form, then create calls to the corresponding chain of
     * Rust constructors. Or, we can take advantage of having modeled
     * the internal representation used on the Rust side to just
     * extract the internal representation and transfer that across.
     *
     * There are at least three reasons to prefer the latter:
     *    - the internal representation is a u64 and is therefore
     *      easily moved across;
     *    - we can construct calls to arbitrary Rust functions if
     *      needed, but using them in spec-level logic is at least
     *      problematic (not sure whether it's allowed at all);
     *    - when we get to supporting object-style values, the
     *      external pointer part can just be pushed across as part
     *      of the u64 and we don't need to process the referent.
     *
     * In fact, I think this is why the Cryptol-level value logic was
     * built to match the internals of the Rust value logic.
     *
     * Ergo, the way this works is:
     *    - for types that aren't Values::Raw::T, recurse on the raw
     *      value and pass the results to the corresponding Rust
     *      constructor;
     *    - for Values::Raw::T, extract the bits of the representation
     *      and pass that to the Rust constructor.
     *
     * Note that for the latter case we need to fetch the bits
     * directly rather than calling the Cryptol-level accessor
     * function. Otherwise SAW loses track of the value when doing
     * override matching and the specs we produce don't work. See
     * saw-script #2035.
     *
     * For reasons I don't understand this seems to apply only to
     * the raw layer; the accessor functions for the wrapper types
     * seem to work ok.
     */
    rec to_mir_value x =
      let t = type x in
      if type_eq t {| Values::VoidT |} then
        mir_struct_value (soroban_adt.Void ()) [
          to_mir_value {{ Values::void_to_raw x }}
        ]
      else if type_eq t {| Values::BoolT |} then
        mir_struct_value (soroban_adt.Bool_ ()) [
          to_mir_value {{ Values::bool_to_raw x }}
        ]
      else if type_eq t {| Values::U32T |} then
        mir_struct_value (soroban_adt.U32Val ()) [
          to_mir_value {{ Values::u32_to_raw x }}
        ]
      else if type_eq t {| Values::SymbolT |} then
        mir_struct_value (soroban_adt.Symbol ()) [
          mir_value.MaybeEnv (),
          mir_struct_value (soroban_adt.SymbolVal ()) [
            to_mir_value {{ Values::symbol_to_raw x }}
          ]
        ]
      else if type_eq t {| Values::RawT |} then
        mir_struct_value (soroban_adt.RawVal ()) [
          // XXX: can't do this, it makes SAW unable to match specs
          //mir_term {{ Values::raw_get_payload x }}
          mir_term {{ x.bits }}
        ]
      else
        run (fail (str_concat (show t) " cannot be converted to a MIR value"));

    /*
     * Extract the MIR-level type for type T.
     *
     * This function is the type-level version of to_mir_value, and
     * uses the same types as returned by to_native.
     *
     * XXX: except that's wrong... and we maybe need name reform,
     * because there's _three_ types for any given Soroban value:
     *    - the Rust type, which is something out of the MIR;
     *    - the Cryptol type, which matches the structure of the MIR type;
     *    - the native SAW and/or Cryptol representation of the value;
     *
     * and I think we're playing fast and loose with the distinction
     * between the last two, which is bad.
     */
    let to_mir_type t =
      if type_eq t {| Values::VoidT |} then
        mir_adt (soroban_adt.Void ())
      else if type_eq t {| Values::BoolT |} then
        mir_adt (soroban_adt.Bool_ ())
      else if type_eq t {| Values::U32T |} then
        mir_u32
      else if type_eq t {| Values::SymbolT |} then
        mir_adt (soroban_adt.Symbol ())
      else if type_eq t {| Values::RawT |} then
        mir_adt (soroban_adt.RawVal ())
      else
        run (fail (str_concat (show t) " cannot be converted to MIRType"));

    /*
     * Spec for the storage interface.
     *
     * For now at least the Cryptol-level store is written
     * purely in terms of Values::Raw::T and the type wrapping happens
     * here. It's possible that should change, but we'll see.
     */
    let spec_storage = {

      state = \store -> do {
        mir_ghost_value state.storage store;
      },

      has = \store k ->
        let k_to_raw = to_raw (type k) in
        {{ Store::has store (k_to_raw k) }},

      get = \store k tv ->
        let k_to_raw = to_raw (type k) in
        let v_from_raw = from_raw tv in
        {{ Option::fmap
            (Store::get store (k_to_raw k))
            v_from_raw }},

      set = \store k v ->
        let k_to_raw = to_raw (type k) in
        let v_to_raw = to_raw (type v) in
        {{ Store::set store (k_to_raw k) (v_to_raw v) }}

    };

    /*
     * Wrapper for contract specs.
     */
    let soroban_spec spec_name spec = do {

      let (api : SorobanSpecAPI) = {

        storage = spec_storage,

        /*
         * Generate a fresh var of the requested value type.
         */
        fresh = \name t -> do {
          x <- mir_fresh_cryptol_var (str_concats [spec_name, "::", name]) t;
          let t_valid = valid t;
          mir_assert {{ t_valid x }};
          return x;
        },

        mir_value = mir_value,
        to_mir_value = to_mir_value,
        to_mir_type = to_mir_type

      };

      spec api;

    };

    let sdk_assume feat name spec =
      if string_list.elem feat features then
        list.sequence [mir_unsafe_assume_spec mod name (soroban_spec name spec)]
      else
        return [];

    /*
     * Construct the "overrides" (checked or admitted SAW theorems)
     * for the SDK interfaces.
     */
    sdk_overridess <-

      let adt_Infallible = mir_find_adt mod "core::convert::Infallible" [] in
      let adt_ResultInfallible (t : MIRType) =
        mir_find_adt mod "core::result::Result" [t, mir_adt adt_Infallible] in

      let return_ok (x : Term) : MIRSetup () =
        mir_return
          (mir_enum_value
            (adt_ResultInfallible (to_mir_type (type x)))
            "Ok"
            [to_mir_value x])
      in

      list.sequence [

        sdk_assume "storage" "soroban_sdk::env::{impl#12}::has_contract_data"
          (\(s : SorobanSpecAPI) -> do {

            store <- mir_fresh_cryptol_var "storage.has.store" {| Store::T |};

            self <- mir_alloc (mir_adt (soroban_adt.Env ()));
            mir_points_to self (s.mir_value.Env ());

            k <- s.fresh "k" {| Values::RawT |};

            s.storage.state store;

            mir_execute_func [self, s.to_mir_value k];

            s.storage.state store;

            return_ok {{ Values::bool_from_native (Store::has store k) }};

          }),

        sdk_assume "storage" "soroban_sdk::env::{impl#12}::get_contract_data"
          (\(s : SorobanSpecAPI) -> do {

            store <- mir_fresh_cryptol_var "storage.get.store" {| Store::T |};

            self <- mir_alloc (mir_adt (soroban_adt.Env ()));
            mir_points_to self (s.mir_value.Env ());

            k <- s.fresh "k" {| Values::RawT |};

            mir_precond {{ Store::has store k }};
            s.storage.state store;

            mir_execute_func [self, s.to_mir_value k];

            s.storage.state store;

            return_ok {{ (Store::get store k).value }};

          }),

        sdk_assume "storage" "soroban_sdk::env::{impl#12}::put_contract_data"
          (\(s : SorobanSpecAPI) -> do {

            store <- mir_fresh_cryptol_var "storage.put.store" {| Store::T |};

            self <- mir_alloc (mir_adt (soroban_adt.Env ()));
            mir_points_to self (s.mir_value.Env ());

            k <- s.fresh "k" {| Values::RawT |};
            v <- s.fresh "v" {| Values::RawT |};

            s.storage.state store;

            mir_execute_func [self, s.to_mir_value k, s.to_mir_value v];

            s.storage.state {{ Store::set store k v }};

            return_ok {{ Values::unit }};

          })

      ];
    let sdk_overrides = list.concats sdk_overridess;

    /*
     * Hook for running verification
     */
    let soroban_verify (args : SorobanVerifyArgs) =
      mir_verify mod args.fun (concat sdk_overrides args.overrides)
        args.path_sat args.spec args.proof;

    /*
     * Hook for assuming a specification instead
     * (intentionally has the same signature, even though some of it
     * isn't used)
     */
    let soroban_assume (args : SorobanVerifyArgs) =
      mir_unsafe_assume_spec mod args.fun args.spec;

    return {
      adt = soroban_adt,
      spec = soroban_spec,
      verify = soroban_verify,
      assume = soroban_assume
    };

  };

  return soroban_init;

};
